PREFIX wd: <https://catalog.digital-scriptorium.org/entity/>
PREFIX wds: <https://catalog.digital-scriptorium.org/entity/statement/>
PREFIX wdv: <https://catalog.digital-scriptorium.org/value/>
PREFIX wdt: <https://catalog.digital-scriptorium.org/prop/direct/>
PREFIX p: <https://catalog.digital-scriptorium.org/prop/>
PREFIX ps: <https://catalog.digital-scriptorium.org/prop/statement/>
PREFIX pq: <https://catalog.digital-scriptorium.org/prop/qualifier/>

PREFIX schema: <http://schema.org/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

# find names associated with manuscript records describing any production place
SELECT ?place ?placeLabel ?placeString ?record ?recordLabel ?nameString ?name ?nameLabel ?QID
WHERE {
# bind query variables
  BIND(p:P27 AS ?placeAsRecordedStatement).
  BIND(ps:P27 AS ?placeAsRecorded).
  BIND(pq:P28 AS ?hasPlace).
  BIND(wdt:P44 AS ?hasExternalURI).
  BIND(p:P14 AS ?nameAsRecordedStatement).
  BIND(ps:P14 AS ?nameAsRecorded).
  BIND(pq:P17 AS ?hasName).
  BIND(wdt:P42 AS ?hasQID).
# statement: manuscript record has statement for place recorded as literal string value 
  ?record ?placeAsRecordedStatement ?placeStatement .
# statement: place statement has place object recorded as string value
  ?placeStatement ?placeAsRecorded ?placeString .
# statement: place statement has qualifier for place authority value
  ?placeStatement ?hasPlace ?place .
# statement: manuscript record has statement for name recorded as literal string value
  ?record ?nameAsRecordedStatement ?nameStatement .
# statement: name statement has name object recorded as string value
  ?nameStatement ?nameAsRecorded ?nameString .
# statement: name statement has qualifier for possible structured/authority value, which may have QID
  OPTIONAL { { ?nameStatement ?hasName ?name .} OPTIONAL { ?name ?hasQID ?QID .} }
#   
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE]". }
  }
